CompUnit      ::= {CompUnit} (BlockItem | FuncDef);

Number        ::= INT_CONST;
Exp           ::= LOrExp;
String        ::= "\"" STR_CONST "\"";
Char          ::= "\'" CHAR_CONST "\'";
Boolean       ::= "TRUE" | "FALSE";
Date          ::= [0-9]{2}[-/.][0-9]{2}[-/.][0-9]{4};

(* Const & Variable *)
Decl          ::= ConstDecl | VarDecl;
BType         ::= "INTEGER" | "REAL" | "CHAR" | "STRING" | "BOOLEAN" | "DATE";
ConstDecl     ::= "CONSTANT" IDENTIFIER "=" ConstInitVal;
ConstInitVal  ::= Exp | String | Char | Boolean | Date;
VarDecl       ::= "DECLARE" IDENTIFIER ":" BType;

(* Function *)
FuncDef       ::= FUNCTION IDENTIFIER "(" [ ParamList ] ")" RETURNS FuncType Block ENDFUNCTION;
ParamList     ::= Param { "," Param };
Param         ::= IDENTIFIER ":" BType;
FuncType      ::= BType | UserDefType;
UserDefType   ::= IDENTIFIER;

Block         ::= { BlockItem };
BlockItem     ::= Decl | Stmt;
Stmt          ::= LVal "<-" Exp
              | "IF" Exp "THEN" Block ["ELSE" Block] "ENDIF"
              | "WHILE" Exp Block "ENDWHILE"
              | "RETURN" Exp;

(* Expression *)
LVal          ::= IDENTIFIER;
PrimaryExp    ::= "(" Exp ")" | Number | LVal;
UnaryExp      ::= PrimaryExp | UnaryOp UnaryExp;
UnaryOp       ::= "+" | "-" | "NOT";
MulExp        ::= UnaryExp | MulExp ("*" | "/" | "DIV" | "MOD") UnaryExp;
AddExp        ::= MulExp | AddExp ("+" | "-") MulExp;
RelExp        ::= AddExp | RelExp ("<" | ">" | "<=" | ">=") AddExp;
EqExp         ::= RelExp | EqExp ("=" | "<>") RelExp;
LAndExp       ::= EqExp | LAndExp "AND" EqExp;
LOrExp        ::= LAndExp | LOrExp "OR" LAndExp;
